#Использовать asserts
#Использовать logos
#Использовать tempfiles
#Использовать fs
#Использовать "../src"

Перем юТест; 	// Движок тестов
Перем Лог;		// Текущий лог
Перем МенеджерВременныхФайлов;	// Для управления временными файлами

// Основная точка входа
Функция ПолучитьСписокТестов(ЮнитТестирование) Экспорт
	
	ПередЗапускомТестов();
	
	юТест = ЮнитТестирование;
	
	ВсеТесты = Новый Массив;
	  
	ВсеТесты.Добавить("Тест_ПолучениеРесурсов");
	ВсеТесты.Добавить("Тест_РазмерФайла");
	ВсеТесты.Добавить("Тест_СериализацияИДесериализация");
	
	Возврат ВсеТесты;
	
КонецФункции

Процедура ПередЗапускомТестов()
	
	Попытка
		ВремТестер = Новый Тестер;
		Лог = Логирование.ПолучитьЛог(ВремТестер.ИмяЛога());
	Исключение
		Лог = Логирование.ПолучитьЛог("Test");
	КонецПопытки;
	
КонецПроцедуры

Процедура ПередЗапускомТеста() Экспорт
	
	ПараметрыПриложения.УстановитьРежимОтладки(Истина);
	
	МенеджерВременныхФайлов = Новый МенеджерВременныхФайлов;
	
КонецПроцедуры

Процедура ПослеЗапускаТеста() Экспорт
	
	МенеджерВременныхФайлов.Удалить();
	МенеджерВременныхФайлов = Неопределено;
	
КонецПроцедуры

Процедура Тест_ПолучениеРесурсов() Экспорт
	
	Файл = Новый Файл(ФайловыеОперации.Ресурсы());

	Ожидаем.Что(Файл.Существует(), "Директории не существует").ЭтоИстина();
	Ожидаем.Что(Файл.ЭтоКаталог(), "Не является директорией").ЭтоИстина();
	Ожидаем.Что(НайтиФайлы(ФайловыеОперации.Ресурсы(), "*.json").Количество(), "Изменилось количество ресурсов").Равно(3);

КонецПроцедуры

Процедура Тест_СериализацияИДесериализация() Экспорт

	ПутьКФайлу = МенеджерВременныхФайлов.СоздатьФайл("test.json");

	Объект = ФайловыеОперации.ПрочитатьОбъект(ФайловыеОперации.РесурсОтчет(), Истина);
	Ожидаем.Что(ТипЗнч(Объект), "Типы не совпали").Равно(Тип("Соответствие"));

	ФайловыеОперации.ЗаписатьОбъект(ПутьКФайлу, Объект);

КонецПроцедуры

Процедура Тест_РазмерФайла() Экспорт
	
	ПутьКФайлу = МенеджерВременныхФайлов.СоздатьФайл("module.bsl");

	Пустота = ФайловыеОперации.ПустойФайл(ПутьКФайлу);
	Ожидаем.Что(Пустота, "Ожидали что файл пустой").ЭтоИстина();
	
	ПутьКФайлу = МенеджерВременныхФайлов.СоздатьФайл("module1.bsl");

	Файл = Новый ЗаписьТекста(ПутьКФайлу);
	Файл.ЗаписатьСтроку(Символы.НПП);
	Файл.Закрыть(); 
	Пустота = ФайловыеОперации.ПустойФайл(ПутьКФайлу);
	Ожидаем.Что(Пустота, "Ожидали что файл не пустой").ЭтоЛожь();

	ПутьКФайлу = МенеджерВременныхФайлов.СоздатьФайл("module2.bsl");

	Файл = Новый ЗаписьТекста(ПутьКФайлу);
	Файл.ЗаписатьСтроку("//");
	Файл.Закрыть();
	Пустота = ФайловыеОперации.ПустойФайл(ПутьКФайлу);
	Ожидаем.Что(Пустота, "Ожидали что файл не пустой").ЭтоЛожь();	

КонецПроцедуры